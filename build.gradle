/*
MIT License

Copyright (c) 2016 Thought Logix

Permission is hereby granted, free of charge, to any person obtaining a copy
of this software and associated documentation files (the "Software"), to deal
in the Software without restriction, including without limitation the rights
to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
copies of the Software, and to permit persons to whom the Software is
furnished to do so, subject to the following conditions:

The above copyright notice and this permission notice shall be included in all
copies or substantial portions of the Software.

THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
SOFTWARE.
*/

group 'com.thoughtlogix'
version '1.0-SNAPSHOT'

////////////////////////////////////////////////////////////////////
// COMMON BUILD SCRIPT SETUP
////////////////////////////////////////////////////////////////////
buildscript {
    ext.kotlin_version = '1.0.3'
    ext.spark_version = '2.5'

    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

apply plugin: 'java'
apply plugin: 'kotlin'
apply plugin: 'application'
apply plugin: 'project-report'
apply plugin: 'idea'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    compile("com.sparkjava:spark-core:$spark_version")
    compile 'com.mitchellbosecke:pebble:2.2.2'
    compile group: 'com.sparkjava', name: 'spark-debug-tools', version: '0.5'
    compile group: 'org.slf4j', name: 'slf4j-simple', version: '1.6.2'
    testCompile group: 'org.testng', name: 'testng', version: '6.9.10'
    testCompile 'org.fluentlenium:fluentlenium-core:0.13.1'
    testCompile 'org.fluentlenium:fluentlenium-testng:0.13.1'
    testCompile 'org.assertj:assertj-core:3.4.1'
    testCompile 'org.seleniumhq.selenium:selenium-java:2.52.0'
    testCompile 'org.seleniumhq.selenium:selenium-htmlunit-driver:2.52.0'
}


////////////////////////////////////////////////////////////////////
// TESTING STUFF
////////////////////////////////////////////////////////////////////
dependencies {
    testCompile 'io.kotlintest:kotlintest:1.3.1'
    testCompile group: 'org.testng', name: 'testng', version: '6.9.10'
}

test {
    minHeapSize = "256m"
    maxHeapSize = "1024m"
    testLogging.showStandardStreams = true
    beforeTest { descriptor ->
        logger.lifecycle("Running test: " + descriptor)
    }
}

task testingUnit(type: Test, dependsOn: 'test') {
    group = 'sparkstarter'
    useTestNG() {
        excludeGroups 'performance', 'integration', 'concurrency'
    }
}

task testingIntegration(type: Test, dependsOn: 'test') {
    group = 'sparkstarter'
    useTestNG() {
        includeGroups 'integration'
    }
}

task testingPerformance(type: Test, dependsOn: 'test') {
    group = 'sparkstarter'
    useTestNG() {
        includeGroups 'performance'
    }
}

task testingConcurrency(type: Test, dependsOn: 'test') {
    group = 'sparkstarter'
    useTestNG() {
        includeGroups 'concurrency'
    }
}


////////////////////////////////////////////////////////////////////
// APPLICATION PLUGIN CONFIG
////////////////////////////////////////////////////////////////////
mainClassName = "com.example.MainKt"


////////////////////////////////////////////////////////////////////
// CUSTOM TASKS
////////////////////////////////////////////////////////////////////
task runServer(type: JavaExec) {
    group = 'sparkstarter'
    main = "com.example.MainKt"
    classpath = sourceSets.main.runtimeClasspath
    maxHeapSize = "1024m"
    dependsOn(build)
}

////////////////////////////////////////////////////////////////////
// HELPER TASKS
////////////////////////////////////////////////////////////////////
task _copyToLib(type: Copy) {
    into "$buildDir/libs"
    from configurations.runtime
}

task _copyToDist(type: Copy) {
    into "dist/lib"
    from "$buildDir/libs"
}
build.dependsOn([_copyToLib, _copyToDist])

tasks.withType(JavaCompile) {
    options.warnings = false
    options.deprecation = false
    options.verbose = false
    options.failOnError = false
}
